
-- programs ------------------------------------------------

entrypoints Prog ;

Program.   Prog ::= Term ;



-- Atoms -----------------------------------------------------


AVar.      Atom ::= Ident;

ATerm.     Atom  ::= "(" Term ")";

ACons.     Atom ::= "cons" Type ;

-- Expression ----------------------------------------------
ApplicationAtom.  Application ::= Atom;

AApplication.     Application ::= Application Atom;



-- Term ---------------------------------------------------------------

ApplicationTerm.     Term ::= Application;

LetInTerm.           Term ::= "let" Ident "=" Term "in" Term;

LambdaAbsTerm.       Term ::= "lam" Ident ":" Type "." Term;

TrueTerm.            Term ::= "true";

FalseTerm.           Term ::= "false";

IfTerm.              Term ::= "if" Term "then" Term "else" Term;

SuccTerm.            Term ::= "succ" Term;

ZeroTerm.            Term ::= "zero";

PredTerm.            Term ::= "pred" Term; 

IsZeroTerm.          Term ::= "isZero" Term; 

PairTerm.            Term ::= "{" Term "," Term "}";

FstTerm.             Term ::= "first" Term; 

SndTerm.             Term ::= "second" Term;

NilTerm.             Term ::= "nil" Type ;

IsNilTerm.           Term ::= "isnil" Type Term;

HeadTerm.            Term ::= "head" Type Term;

TailTerm.            Term ::= "tail" Type Term;

FixTerm.             Term ::= "fix" Term; 

IsSuccTerm.          Term ::= "isSucc" Term; 

IsPredTerm.          Term ::= "isPred" Term;



-- Types ---------------------------------------------------
-- Lowest precedence (whole Type)
Arrow.    Type  ::= BaseType "->" Type ;
Base.     Type  ::= BaseType ;


-- Higher precedence
Pair.     BaseType  ::= "{" Type "," Type "}" ;
Nat.      BaseType  ::= "Nat" ;
Bool.     BaseType  ::= "Bool" ;
ListT.    BaseType  ::= "[" Type "]" ;


coercions  Type 2;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

-- Tokens --------------------------------------------------
